subprojects {
    plugins.withType(JavaPlugin).whenPluginAdded {
        group= 'br.com.gamemods.minecity'
        version = '1.0.A.3-SNAPSHOT'

        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
        }

        configurations {
            provided
        }

        sourceSets.main { compileClasspath += configurations.provided }

        dependencies {
            compile 'org.jetbrains:annotations:13.0'

            testCompile 'junit:junit:4.12'
            testCompile 'org.powermock:powermock-module-junit4:1.6.5'
            testCompile 'org.powermock:powermock-api-mockito:1.6.5'
            testCompile project(':UnitTest')
            testCompile 'mysql:mysql-connector-java:5.1.32'
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = 'gamemods-nexus'
            url = 'http://nexus.gamemods.com.br/content/groups/public/'
        }
        maven {
            name = 'spigot-nexus'
            url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
        }
        maven {
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
        maven {
            name = 'Plugin Metrics'
            url = 'http://repo.mcstats.org/content/repositories/public'
        }
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = 'vault-repo'
            url = 'http://nexus.hc.to/content/repositories/pub_releases'
        }
        maven {
            url = 'https://jitpack.io'
        }
    }
}


project(':Core') {
    apply plugin: 'java'
    dependencies {
        provided 'com.google.code.findbugs:jsr305:1.3.9'
        testCompile 'org.luaj:luaj-jse:3.0.1'
    }
}

project(':Reactive') {
    apply plugin: 'java'
    dependencies {
        compile project(':Core')
        compile 'org.codehaus.groovy:groovy-all:2.4.7'
        provided 'com.google.code.findbugs:jsr305:1.3.9'
    }

    sourceSets {
        main.resources.srcDirs += 'src/main/scripts'
    }

    jar {
        exclude('minecity')
        into 'minecity/scripts/minecity/', {
            from 'src/main/scripts/minecity'
        }
    }
}

project(':UnitTest') {
    apply plugin: 'java'
    dependencies {
        compile 'org.assertj:assertj-core:3.1.0'
    }
}
